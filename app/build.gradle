buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.1'
    }

}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
repositories {
    maven { url 'https://maven.fabric.io/public' }
    google()
    jcenter()
}

apply plugin: 'checkstyle'
apply plugin: 'pmd'

android {
    compileSdkVersion 24
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.danielme.muspyforandroid"
        minSdkVersion 18
        targetSdkVersion 22
        versionCode 7
        versionName "2.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

task checkstyle(type: Checkstyle) {
    group 'verification'
    source 'src'
    configFile file("${project.rootDir}/quality/checkstyle.xml")
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/build/**'
    exclude '**/bin/**'
    exclude '**/libs/**'
    exclude '**/test/**'
    exclude '**/androidTest/**'
    classpath = files()
    showViolations true
    reports {
        xml.destination "$project.buildDir/reports/checkstyle.xml"
        html.destination "$project.buildDir/reports/checkstyle.html"
    }
}

task pmd(type: Pmd) {
    group 'verification'
    ruleSetFiles = files("${project.rootDir}/quality/pmd.xml")
    ignoreFailures = false
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/test/**'
    exclude '**/androidTest/**'
    exclude '**/build/**'
    reports {
        xml.enabled = true
        html.enabled = true
        xml {
            destination "$project.buildDir/reports/pmd.xml"
        }
        html {
            destination "$project.buildDir/reports/pmd.html"
        }
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:customtabs:24.2.1'
    compile 'com.android.support:gridlayout-v7:24.2.1'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile ('com.github.franmontiel:PersistentCookieJar:v0.9.3') {
        exclude module: 'com.square'
    }
    compile 'com.android.support:support-annotations:24.2.1'
    compile 'com.github.bumptech.glide:glide:3.8.0'
    compile 'com.android.support:support-v4:24.2.1'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.8.3'
    compile 'com.scottyab:secure-preferences-lib:0.1.4'
    compile 'com.google.dagger:dagger:2.14.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.14.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }
    dependencies {
        implementation 'uk.co.samuelwall:material-tap-target-prompt:2.7.0'
    }
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'junit:junit:4.12'
    androidTestCompile ('com.android.support.test:runner:0.3') {
        exclude module: 'support-annotations'
    }
    androidTestCompile ('com.android.support.test:rules:0.3') {
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:3.0.1') {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'recyclerview-v7'
    }
    androidTestCompile ('com.android.support.test.espresso:espresso-core:3.0.1') {
        exclude module: 'support-annotations'
    }
    androidTestCompile ('com.android.support.test.espresso:espresso-web:3.0.1') {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'recyclerview-v7'
    }

    checkstyle('com.puppycrawl.tools:checkstyle:6.19')
    pmd(
            'net.sourceforge.pmd:pmd-core:5.4.1',
            'net.sourceforge.pmd:pmd-java:5.4.1'
    )
}
